apply plugin: 'com.android.library'
import org.apache.tools.ant.taskdefs.condition.Os


android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 14
        versionName new File(projectDir.path, ('/../../VERSION')).text.trim()
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        resValue "string", "SDL_LIB_VERSION", '\"' + versionName + '\"'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            multiDexEnabled true
        }
        debug {
            testCoverageEnabled = true
            multiDexEnabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    sourceSets {
        if(Os.isFamily(Os.FAMILY_WINDOWS)){
            //The buildWindowsSymLinks task must be run first if this is
            //being compiled on a Windows machine
            main.java.srcDirs += '../../baseAndroid/windows/src/main/java'
        }else{
            main.java.srcDirs += '../../baseAndroid/src/main/java'
        }
    }
}

task buildWindowSymLinks(type:Exec){

    workingDir '../../baseAndroid'

    commandLine 'cmd', '/c', 'make_symlinks.lnk'
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    api 'com.smartdevicelink:bson_java_port:1.2.0'
    api 'com.livio.taskmaster:taskmaster:0.3.0'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.9.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'org.mockito:mockito-core:2.9.0'
    androidTestImplementation 'org.mockito:mockito-android:2.9.0'
    api 'androidx.annotation:annotation:1.0.0'
    api 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    annotationProcessor 'androidx.lifecycle:lifecycle-compiler:2.0.0'
}

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

apply from: 'bintray.gradle'
